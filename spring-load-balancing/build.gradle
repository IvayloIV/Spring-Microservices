plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.15'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
	group 'com.soft2run'
	version '1.0.0'
}

repositories {
	mavenCentral()
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		sourceCompatibility = '17'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	ext {
		set('springCloudVersion', "2021.0.8")
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(":vehicle-without-eureka") {

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
	}
}

project(":station-without-eureka") {

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		implementation 'org.springframework.boot:spring-boot-starter-webflux'
		implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
	}
}

project(":eureka-discovery-server") {

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
	}
}

project(":vehicle-with-eureka") {

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
	}
}

project(":station-with-eureka") {

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		implementation 'org.springframework.boot:spring-boot-starter-webflux'
	}
}
