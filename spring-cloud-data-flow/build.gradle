plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.15'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
	group 'com.soft2run'
	version '1.0.0'
}

repositories {
	mavenCentral()
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		sourceCompatibility = '11'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	ext {
		set('springCloudVersion', "2021.0.8")
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(":data-flow-task") {

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-task'
		implementation 'org.postgresql:postgresql:42.6.0'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	}
}

project(":data-flow-stream") {

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-stream'
		implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
		implementation 'org.springframework.kafka:spring-kafka'
		//Generate metadata for the Jar (Data Flow used it)
		annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
		//Provide endpoints for health and info
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
	}
}
