plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.14-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
	group = 'com.soft2run'
	version = '0.0.1-SNAPSHOT'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		sourceCompatibility = '17'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(':stomp') {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-websocket'
	}
}

project(':websocket') {
	dependencies {
		implementation 'org.springframework:spring-websocket'
	}
}

project(':socket-io') {
	dependencies {
		implementation 'com.corundumstudio.socketio:netty-socketio:2.0.3'
	}
}

bootJar.enabled = false
jar.enabled = false
